/*
 * The MIT License
 * 
 * Copyright (c) 2010-2011 MigDB team [https://rabbit.felk.cvut.cz/trac/migdb]
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 * 
 */

modeltype APP uses 'http://www.collectionspro.eu/jam/mm/reduced-jam';

transformation APP_To_Rdb(in inModel : APP, out outModel : APP);
main() {
	assert(inModel.rootObjects()->size() = 1);
	assert(inModel.rootObjects()->forAll(oclIsKindOf(reduced::ModelRoot)));
	
	log("transformation started...");
	
	inModel.rootObjects().deepclone();

	var root : reduced::ModelRoot = outModel.rootObjects()![reduced::ModelRoot];
	var gen0 : reduced::ModelGeneration = root.modelGenerations->first();
	
	root.operations->forEach(op) {
		log("gen0 " + gen0.repr());
		log("root " + root.repr());
		log("	iterating over operation " + op.repr());
		if (op.isValid(gen0)) then {
			log("	operation is valid " + op.repr());
			op.xmap apply(gen0);
		} else {
			log("	operation is invalid " + op.repr());
			// break;
		} endif;
	};
	
	root.operations = root.operations->reject(true);
}

// kontrola validity operace
abstract query reduced::operations::ModelOperation::
	isValid(gen : reduced::ModelGeneration) : Boolean;

// namapov�n� dan� operace
abstract mapping reduced::operations::ModelOperation::
	apply(inout gen : reduced::ModelGeneration);


/* ++++++++++ ADD OPERATIONS ++++++++++++++++++++ */

query reduced::operations::AddClass::isValid(gen : reduced::ModelGeneration) : Boolean {
	var b : Boolean := true;
	if (self.parent <> null) then { b := gen.isAbstractClassInGeneration(self.parent) and not (self.name = self.parent); } endif;
	return not gen.isAbstractClassInGeneration(self.name) and b;
}

// p�id�n� standardn� t��dy do generace
mapping reduced::operations::AddClass::apply(inout gen : reduced::ModelGeneration) {
	gen.classes += object reduced::Class {
		name := self.name;
		parent := gen.findAbstractClass(self.parent).oclAsType(reduced::Class);
		isAbstract := self.isAbstract;
		isEmbedded := self.isEmbedded;
		tableName := self.tableName;
		inheritanceType := self.inheritanceType;
	};
	
	// vytvo�en� id atributu
	if (self.parent = null) then {
		var c : reduced::Class := gen.findAbstractClass(self.name).oclAsType(reduced::Class);
		c.properties += object reduced::Property {
			name := "id";
			isID := true;
			// [?] nutn� nastavit typ atributu
		};
	} endif;
}

query reduced::operations::AddPrimitiveClass::isValid(gen : reduced::ModelGeneration) : Boolean {
	return not gen.isAbstractClassInGeneration(self.name);
}

// p�id� primitivn� t��du do generace
mapping reduced::operations::AddPrimitiveClass::apply(inout gen : reduced::ModelGeneration) {
	gen.classes += object reduced::PrimitiveClass {
		name := self.name;
		primitiveType := self.primitiveType;
	};
}

query reduced::operations::AddProperty::isValid(gen : reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and not gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class).isPropertyInClass(self.name)
	// ov���, zda-li existuje t��da po�adovan�ho typu atributu
		   and gen.isAbstractClassInGeneration(self.type)
	// ov���, zda-li jsou hranice nastaveny smyslupln�
		   and (self.lowerBound <= self.upperBound or (self.upperBound = -1 and self.lowerBound > self.upperBound))
	// v hierarchii neexistuje atribut se stejn�m n�zvem
		   and not gen.isPropertyInHierarchy(self.owningClassName, self.name);
}

// Pri vytvoreni Property lze vytvorit jednostranne navigabilni asociaci
// lze nastavit i kolekci:
//isOrdered := self.isOrdered; // has to be a collection (upperBound == -1)
//isUnique := self.isUnique; // has to be a collection (upperBound == -1)
mapping reduced::operations::AddProperty::apply(inout gen : reduced::ModelGeneration) {
	var c : reduced::Class := gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class);
	
	c.properties += object reduced::Property {
		name := self.name;
		type := gen.findAbstractClass(self.type);
		defaultValue := self.defaultValue; // a value type has to be equal with a type of property
		lowerBound := self.lowerBound;
		upperBound := self.upperBound;
		isOrdered := self.isOrdered; // has to be a collection (upperBound == -1)
		isUnique := self.isUnique; // has to be a collection (upperBound == -1)
		columnName := self.columnName;
		columnNamePattern := self.columnNamePattern;
		sequenceName := self.sequenceName;
	};
}

query reduced::operations::SetOpposite::isValid(gen : reduced::ModelGeneration) : Boolean {
	var pre : Boolean := 
	// t��dy existuj� v dan� generaci
		gen.isAbstractClassInGeneration(self.firstClassName)
		and gen.isAbstractClassInGeneration(self.secondClassName)
    // atributy existuj� v dan�ch t��d�ch
		and gen.findAbstractClass(self.firstClassName).oclAsType(reduced::Class).isPropertyInClass(self.firstPropertyName)
		and gen.findAbstractClass(self.secondClassName).oclAsType(reduced::Class).isPropertyInClass(self.secondPropertyName);
	if (self.isOpposite = true) then {
		return pre
		// nejedn� se o oboustrann� navigabiln� asociaci
			and gen.findAbstractClass(self.firstClassName).oclAsType(reduced::Class).findProperty(self.firstPropertyName).oppositeProperty = null
			and gen.findAbstractClass(self.secondClassName).oclAsType(reduced::Class).findProperty(self.secondPropertyName).oppositeProperty = null;
	} else {
		return pre
		// jedn� se o oboustrann� navigabiln� asociaci
			and gen.findAbstractClass(self.firstClassName).oclAsType(reduced::Class).findProperty(self.firstPropertyName).oppositeProperty.name = self.secondPropertyName
			and gen.findAbstractClass(self.secondClassName).oclAsType(reduced::Class).findProperty(self.secondPropertyName).oppositeProperty.name = self.firstPropertyName;
	} endif;
	
	// nedosa�iteln� stav
	return null;
}

mapping reduced::operations::SetOpposite::apply(inout gen : reduced::ModelGeneration) {
	var p1 : reduced::Property := gen.findAbstractClass(self.firstClassName).oclAsType(reduced::Class).findProperty(self.firstPropertyName);
	var p2 : reduced::Property := gen.findAbstractClass(self.secondClassName).oclAsType(reduced::Class).findProperty(self.secondPropertyName);
	if (self.isOpposite = true) then {
		p1.oppositeProperty := p2;
		p2.oppositeProperty := p1;
	} else {
		p1.oppositeProperty := null;
		p2.oppositeProperty := null;
	} endif;	
}


/* ++++++++++ SET OPERATIONS FOR CLASSES ++++++++++++++++++++ */

query reduced::operations::SetAbstract::isValid(gen : reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.name);
}

mapping reduced::operations::SetAbstract::apply(inout gen : reduced::ModelGeneration) {
	var c : reduced::Class := gen.findAbstractClass(self.name).oclAsType(reduced::Class);
	c.isAbstract := self.isAbstract;
}

query reduced::operations::SetEmbedded::isValid(gen : reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.name);
}

mapping reduced::operations::SetEmbedded::apply(inout gen : reduced::ModelGeneration) {
	var c : reduced::Class := gen.findAbstractClass(self.name).oclAsType(reduced::Class);
	c.isEmbedded := self.isEmbedded;
}

query reduced::operations::SetParent::isValid(gen : reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.name)
		   and gen.isAbstractClassInGeneration(self.parentName)
		   and not gen.findAbstractClass(self.parentName).oclAsType(reduced::Class).isDescendantOf(self.name);
}

mapping reduced::operations::SetParent::apply(inout gen : reduced::ModelGeneration) {
	var c : reduced::Class := gen.findAbstractClass(self.name).oclAsType(reduced::Class);
	c.parent := gen.findAbstractClass(self.parentName).oclAsType(reduced::Class);	
}

/* ++++++++++ SET OPERATIONS FOR PROPERTIES ++++++++++++++++++++ */

query reduced::operations::SetType::isValid(gen : reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class).isPropertyInClass(self.name)
		   and gen.isAbstractClassInGeneration(self.type);
}

mapping reduced::operations::SetType::apply(inout gen : reduced::ModelGeneration) {
	var c : reduced::Class := gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class);
	var p : reduced::Property := c.findProperty(self.name);
	p.type := gen.findAbstractClass(self.type); // null
}

query reduced::operations::SetBoundries::isValid(gen : reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class).isPropertyInClass(self.name)
		   // ov���, zda-li jsou hranice nastaveny smyslupln�
		   and (self.lowerBound <= self.upperBound or (self.upperBound = -1 and self.lowerBound > self.upperBound))
}

// nastav� hranice atributu
mapping reduced::operations::SetBoundries::apply(inout gen : reduced::ModelGeneration) {
	var c : reduced::Class := gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class);
	var p : reduced::Property := c.findProperty(self.name);
	// nastav� doln� hranici atributu
	p.lowerBound := self.lowerBound;
	// nastav� horn� hranici atributu
	p.upperBound := self.upperBound;
}

query reduced::operations::SetOrdered::isValid(gen : reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class).isPropertyInClass(self.name);
}

mapping reduced::operations::SetOrdered::apply(inout gen : reduced::ModelGeneration) {
	var c : reduced::Class := gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class);
	var p : reduced::Property := c.findProperty(self.name);
	p.isOrdered := self.isOrdered;
}

query reduced::operations::SetUnique::isValid(gen : reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class).isPropertyInClass(self.name);
}

mapping reduced::operations::SetUnique::apply(inout gen : reduced::ModelGeneration) {
	var c : reduced::Class := gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class);
	var p : reduced::Property := c.findProperty(self.name);
	p.isUnique := self.isUnique;
}


/* ++++++++++ EDIT OPERATIONS ++++++++++++++++++++ */

query reduced::operations::RenameClass::isValid(gen : reduced::ModelGeneration) : Boolean {
	// t��da s n�zvem @name existuje v generaci
	return gen.isAbstractClassInGeneration(self.name)
	// t��da s n�zvem @newName neexistuje v generaci
		   and not gen.isAbstractClassInGeneration(self.newName);
}

mapping reduced::operations::RenameClass::apply(inout gen : reduced::ModelGeneration) {
	var c : reduced::AbstractClass := gen.findAbstractClass(self.name);
	// zm�na hodnoty atributu name
	c.name := self.newName;
}

query reduced::operations::RenameProperty::isValid(gen : reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class).isPropertyInClass(self.name)
		   and not gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class).isPropertyInClass(self.newName);
}

mapping reduced::operations::RenameProperty::apply(inout gen : reduced::ModelGeneration) {
	var c : reduced::Class := gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class);
	var p : reduced::Property := c.findProperty(self.name);
	p.name := self.newName;
}

/* ++++++++++ REMOVE OPERATIONS ++++++++++++++++++++ */

query reduced::operations::RemoveClass::isValid(gen : reduced::ModelGeneration) : Boolean {
	// t��da existuje v dan� generaci
	return gen.isAbstractClassInGeneration(self.name)
	// t��d� neobsahuje ��dn� atributy
	       and (gen.findAbstractClass(self.name).oclAsType(reduced::Class).properties->isEmpty()
	// nebo obsahuje pr�v� jeden s vlastnost� 'id'
	       		or gen.findAbstractClass(self.name).oclAsType(reduced::Class).properties->forAll(isID = true))
	// na t��du se neodkazuje ��dn� jin� t��da
		   and not gen.classes->oclAsType(reduced::Class).properties->exists(type = self)
	// t��da nen� v hierarchii
		   and not gen.classes->oclAsType(reduced::Class)->exists(parent.name = self.name);
}

// odstran� t��du z dan� generace
mapping reduced::operations::RemoveClass::apply(inout gen : reduced::ModelGeneration) {
	// odstran�n� t��dy
	gen.classes := gen.classes->excluding(gen.findAbstractClass(self.name));
}

query reduced::operations::RemoveProperty::isValid(gen : reduced::ModelGeneration) : Boolean {
    // atribut existuje v dan� t��d�
	return gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class).isPropertyInClass(self.name)
	// atribut nem� vlastnost 'id'
		   and gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class).findProperty(self.name).isID = false
	// neexistuje asociace z jin� t��dy
		   and gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class).findProperty(self.name).oppositeProperty = null;
}

// odstran� atribut z dan� t��dy
mapping reduced::operations::RemoveProperty::apply(inout gen : reduced::ModelGeneration) {
	var c : reduced::Class := gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class);
	// odstran�n� atributu
	c.properties := c.properties->excluding(c.findProperty(self.name));
}

query reduced::operations::RemovePrimitiveClass::isValid(gen : reduced::ModelGeneration) : Boolean {
	// t��da existuje v dan� generaci
	return gen.isAbstractClassInGeneration(self.name)
	// na t��du se neodkazuje ��dn� jin� t��da
		   and not gen.classes->oclAsType(reduced::Class).properties->exists(type = self);
}

// odstran� primitivn� t��du z dan� generace
mapping reduced::operations::RemovePrimitiveClass::apply(inout gen : reduced::ModelGeneration) {
	// odstran�n� t��dy
	gen.classes := gen.classes->excluding(gen.findAbstractClass(self.name));
}

/* ++++++++++ COMPLEX OPERATIONS ++++++++++++++++++++ */

query reduced::operations::CopyProperty::isValid(gen : reduced::ModelGeneration) : Boolean{
	return gen.classes->exists(name = self.owningClassName)
		   and gen.classes->exists(name = self.targetClassName)
		   and gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class).properties->exists(name = self.propertyName)
		   and self.endInOwerload != null // testujeme, zda je nejak nastaveno chovani pri preteceni
		   and self.addValueNull != null // testujeme zda je nejak nastaveno chovani pri mensim poctu instanci
		   // musime jeste otestovat, zda jsou obe tridy v hierarchii
		   // OR pouzima proto, ze nejprve testuji zda je target class parentem a jednou childem (je jedno jak hluboko v hierarchii)
		   and (gen.isInHierarchy(self.owningClassName, self.targetClassName) or gen.isInHierarchy(self.targetClassName, self.owningClassName));
}

/* Operace ma jeste dve property:
	 andInOwerload (true - zkonceni pri preteceni)
	 addValueNull (true - pokud bude mene instanci nez v cilove tride, doplnime hodnoty na NULL; false - hodnoty se dopni na default)
	 
	 WARNING: property jeste nejsou nijak implementovane
*/
mapping reduced::operations::CopyProperty::apply(inout gen : reduced::ModelGeneration){
	var c : reduced::Class := gen.findAbstractClass(self.targetClassName).oclAsType(reduced::Class);
	var p : reduced::Property := gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class).findProperty(self.propertyName);
	
	c.properties += object reduced::Property {
		name := p.name;
		type := p.type;
		defaultValue := p.defaultValue;
		lowerBound := p.lowerBound;
		upperBound := p.upperBound;
		isOrdered := p.isOrdered;
		isUnique := p.isUnique;
		columnName := p.columnName;
		columnNamePattern := p.columnNamePattern;
		sequenceName := p.sequenceName;
	};
}

query reduced::operations::MoveProperty::isValid(gen : reduced::ModelGeneration) : Boolean{
	return gen.classes->exists(name = self.owningClassName)
		   and gen.classes->exists(name = self.targetClassName)
		   and gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class).properties->exists(name = self.propertyName)
		   and self.endInOwerload != null // testujeme, zda je nejak nastaveno chovani pri preteceni
		   and self.addValueNull != null // testujeme zda je nejak nastaveno chovani pri mensim poctu instanci
		   // musime jeste otestovat, zda jsou obe tridy v hierarchii
		   // OR pouzima proto, ze nejprve testuji zda je target class parentem a jednou childem (je jedno jak hluboko v hierarchii)
		   and (gen.isInHierarchy(self.owningClassName, self.targetClassName) or gen.isInHierarchy(self.targetClassName, self.owningClassName));
}

mapping reduced::operations::MoveProperty::apply(inout gen : reduced::ModelGeneration){
	var c : reduced::Class := gen.findAbstractClass(self.targetClassName).oclAsType(reduced::Class);
	var p : reduced::Property := gen.findAbstractClass(self.owningClassName).oclAsType(reduced::Class).findProperty(self.propertyName);
	
	c.properties += object reduced::Property {
		name := p.name;
		type := p.type;
		defaultValue := p.defaultValue;
		lowerBound := p.lowerBound;
		upperBound := p.upperBound;
		isOrdered := p.isOrdered;
		isUnique := p.isUnique;
		columnName := p.columnName;
		columnNamePattern := p.columnNamePattern;
		sequenceName := p.sequenceName;
	};
	
	// smazeme property ze tridy c
	c.properties := c.properties->excluding(c.findProperty(self.propertyName));
	
}

/* ++++++++++ QUERIES FOR OPERATIONS ++++++++++++++++++++ */

// ov���, zda-li v generaci existuje jak�koliv t��da s dan�m jm�nem
query reduced::ModelGeneration::isAbstractClassInGeneration(cName : String) : Boolean {
	return self.classes->exists(name = cName);
}

// ov���, zda-li v generaci existuje standardn� t��da s dan�m jm�nem
query reduced::ModelGeneration::isClassInGeneration(cName : String) : Boolean {
	return self.classes.oclAsType(reduced::Class)->exists(name = cName);
}

// vr�t� standardn� t��du dan�ho jm�na
query reduced::ModelGeneration::findAbstractClass(cName : String) : reduced::AbstractClass {
	 return self.classes->selectOne(name = cName);
}


// ov���, zda-li ve t��d� existuje atribut dan�ho jm�na
query reduced::Class::isPropertyInClass(pName : String) : Boolean {
	return self.properties->exists(name = pName);
}

// vr�t� atribut dan�ho jm�na
query reduced::Class::findProperty(pName : String) : reduced::Property {
	return self.properties->selectOne(name = pName);
}


// ov���, zda-li je t��da potomkem t��dy cName
query reduced::Class::isDescendantOf(cName : String) : Boolean {
	if (self.parent <> null and self.parent.name = cName) then return true
	else return self.parent.isDescendantOf(cName) endif;
	return false;
}

// ov���, zda-li je t��da sClass rodi�em t��dy fClass
query reduced::ModelGeneration::isInHierarchy(fClass : String, sClass : String) : Boolean {
	var ourClass : reduced::Class := self.findAbstractClass(fClass).oclAsType(reduced::Class);
	if(ourClass <> null) then {
		if (ourClass.parent <> null and ourClass.parent = sClass) then {
			return true;
		} endif;
		if (ourClass.parent <> null) then {
			return self.isInHierarchy(ourClass.parent.name, sClass);
		} endif;
	}endif;
	return false;
}

// ov���, zda-li v hierarchii existuje atribut se stejn�m n�zvem
query reduced::ModelGeneration::isPropertyInHierarchy(cName : String, pName : String) : Boolean {
	// pro ka�d� atribut se stejn�m n�zvem...
	self.classes->oclAsType(reduced::Class).properties->forEach(p | p.name = pName) {
		// ...ov���me, zda-li se t��dy, kter�m tyto atributy n�le��, nach�z� ve stejn� hierarchii
		if (self.isInHierarchy(cName, p.owningClass.name) = true or self.isInHierarchy(p.owningClass.name, cName) = true) then {
			return true;
		} endif;
	};
	return false;
}
