/*
 * The MIT License
 * 
 * Copyright (c) 2010-2011 MigDB team [https://rabbit.felk.cvut.cz/trac/migdb]
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 * 
 */

modeltype RDB uses rdb('http://www.collectionspro.eu/jam/mm/rdb');

transformation DB_To_Rdb(in inModel : RDB, out outModel : RDB);
main() {
	assert(inModel.rootObjects()->size() = 1);
	assert(inModel.rootObjects()->forAll(oclIsKindOf(RDB::ModelRoot)));
	
	log("transformation started...");
	
	inModel.rootObjects().deepclone();

	var root : RDB::ModelRoot = outModel.rootObjects()![RDB::ModelRoot];
	var gen0 : RDB::ModelGeneration = root.modelGenerations->first();
	
	root.operations->forEach(op) {
		log("	iterating over operation " + op.repr());
		if (op.isValid(gen0)) then {
			log("	operation is valid " + op.repr());
			op.xmap apply(gen0);
		} else {
			log("	operation is invalid " + op.repr());
		} endif;
	};
	
	root.operations = root.operations->reject(true);
}

// kontroluje validitu operace
abstract query RDB::operations::ModelOperation::
	isValid(gen : RDB::ModelGeneration) : Boolean;

// namapuje danou operaci
abstract mapping RDB::operations::ModelOperation::
	apply(inout gen : RDB::ModelGeneration);


/* ++++++++++ ADD OPERATIONS ++++++++++++++++++++ */

query RDB::operations::AddSchema::isValid(gen : RDB::ModelGeneration) : Boolean {
	// v dané generaci neexistuje schéma stejného názvu
	return not gen.isSchemaInGeneration(self.name);
}

// pøidá schéma do generace
mapping RDB::operations::AddSchema::apply(inout gen : RDB::ModelGeneration) {
	gen.schemas += object RDB::Schema {
		name := self.name;
	}
}

query RDB::operations::AddSequence::isValid(gen : RDB::ModelGeneration) : Boolean {
	// v daném schématu neexistuje sekvence stejného názvu
	return not gen.isSequenceInSchema(self.owningSchemaName, self.name);
}

// pøidá sekvenci do schématu
mapping RDB::operations::AddSequence::apply(inout gen : RDB::ModelGeneration) {
	var s : RDB::Schema := gen.findSchema(self.owningSchemaName);
	s.sequences += object RDB::Sequence {
		name := self.name;
		cacheSize := self.cacheSize;
	}
}

query RDB::operations::AddIndex::isValid(gen : RDB::ModelGeneration) : Boolean {
	// v daném schématu neexistuje index stejného názvu
	return not gen.isIndexInSchema(self.owningSchemaName, self.name)
	// v dané tabulce existují dané sloupce
		   and gen.areColumnsInTable(self.owningSchemaName, self.owningTableName, self.columnsNames);
}

// pøidá index do schématu
mapping RDB::operations::AddIndex::apply(inout gen : RDB::ModelGeneration) {
	var s : RDB::Schema := gen.findSchema(self.owningSchemaName);
	s.indexes += object RDB::Index {
		name := self.name;		
		columns += gen.findColumns(self.owningSchemaName, self.owningTableName, self.columnsNames);
	}
}

query RDB::operations::AddTable::isValid(gen : RDB::ModelGeneration) : Boolean {
	// v daném schématu neexistuje tabulka stejného názvu
	return not gen.isTableInSchema(self.owningSchemaName, self.name);
}

// pøidá tabulku do schématu
mapping RDB::operations::AddTable::apply(inout gen : RDB::ModelGeneration) {
	var s : RDB::Schema := gen.findSchema(self.owningSchemaName);
	s.tables += object RDB::Table {
		name := self.name;
	};
}

query RDB::operations::AddColumn::isValid(gen : RDB::ModelGeneration) : Boolean {
	// v dané tabulce neexistuje sloupec stejného názvu
	return not gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.name);
}

// pøidá sloupec do tabulky
mapping RDB::operations::AddColumn::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.columns += object RDB::Column {
		name := self.name;
		type := self.type;
		defaultValue := self.defaultValue;
	}
}

query RDB::operations::AddUniqueIndex::isValid(gen : RDB::ModelGeneration) : Boolean {
	// pro danou tabulku neexistuje omezení stejného názvu
	return not gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name)
	// v daném schématu existuje daný index
	and gen.isIndexInSchema(self.owningSchemaName, self.underlyingIndexName);
}

// pøidá omezení 'UniqueIndex' pro tabulku
mapping RDB::operations::AddUniqueIndex::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.constraints += object RDB::UniqueIndex {
		name := self.name;
		underlyingIndex := gen.findIndex(self.owningSchemaName, self.underlyingIndexName);
	}
}

query RDB::operations::AddPrimaryKey::isValid(gen : RDB::ModelGeneration) : Boolean {
	// pro danou tabulku neexistuje omezení stejného názvu
	return not gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name)
	// v daném schématu existuje daný index
	and gen.isIndexInSchema(self.owningSchemaName, self.underlyingIndexName);
}

// pøidá omezení 'PrimaryKey' pro tabulku
mapping RDB::operations::AddPrimaryKey::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.constraints += object RDB::PrimaryKey {
		name := self.name;
		underlyingIndex := gen.findIndex(self.owningSchemaName, self.underlyingIndexName);
	}
}

query RDB::operations::AddForeignKey::isValid(gen : RDB::ModelGeneration) : Boolean {
	// pro danou tabulku neexistuje omezení stejného názvu
	return not gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name)
	// v daném schématu existuje cílová tabulka
	and gen.isTableInSchema(self.owningSchemaName, self.targetTableName)
	// v cílové tabulce existují dané sloupce
	and gen.isColumnInTable(self.owningSchemaName, self.targetTableName, self.constrainedColumnName);
}

// pøidá omezení 'ForeignKey' pro tabulku
mapping RDB::operations::AddForeignKey::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.constraints += object RDB::ForeignKey {
		name := self.name;
		targetTable := gen.findTable(self.owningSchemaName, self.targetTableName);
		constrainedColumn := gen.findColumn(self.owningSchemaName, self.targetTableName, self.constrainedColumnName);
	}
}

query RDB::operations::AddNotNullConstraint::isValid(gen : RDB::ModelGeneration) : Boolean {
	// pro daný sloupec neexistuje omezení stejného názvu
	return not gen.isConstraintForColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName, self.name);	   
}

// pøidá omezení 'NotNull' pro sloupec
mapping RDB::operations::AddNotNullConstraint::apply(inout gen : RDB::ModelGeneration) {
	var c : RDB::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName);
	c.constraints += object RDB::NotNullConstraint {
		name := self.name;
	}
}

/* ++++++++++ EDIT OPERATIONS ++++++++++++++++++++ */

query RDB::operations::RenameTable::isValid( gen : RDB::ModelGeneration) : Boolean {
	// tabulka s názvem @name existuje ve schématu
	return gen.isTableInSchema(self.owningSchemaName, self.name)
	// tabulka s názvem @newName neexistuje ve schématu
		   and not gen.isTableInSchema(self.owningSchemaName, self.newName);
}

mapping RDB::operations::RenameTable::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.name);
	// zmìna hodnoty atributu name
	t.name := self.newName;
}

query RDB::operations::RenameColumn::isValid(gen : RDB::ModelGeneration) : Boolean {
	// sloupec s názvem @name existuje v tabulce
	return gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.name)
	// sloupec s názvem @newName neexistuje v tabulce
		   and not gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.newName);
}

mapping RDB::operations::RenameColumn::apply(inout gen : RDB::ModelGeneration) {
	var c : RDB::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.name);
	// zmìna hodnoty atributu name
	c.name := self.newName;
}


/* ++++++++++ REMOVE OPERATIONS ++++++++++++++++++++ */

query RDB::operations::RemoveTable::isValid(gen : RDB::ModelGeneration) : Boolean {
	// tabulka existuje v daném schématu
	return gen.isTableInSchema(self.owningSchemaName, self.name)
	// v tabulce existuje pouze identifikaèní sloupec
		   and gen.findTable(self.owningSchemaName, self.name).columns->size() = 1;
}

// odstraní tabulku z daného schématu
mapping RDB::operations::RemoveTable::apply(inout gen : RDB::ModelGeneration) {
	var s : RDB::Schema := gen.findSchema(self.owningSchemaName);
	// odstranìní tabulky
	s.tables := s.tables->excluding(gen.findTable(self.owningSchemaName, self.name));
}

query RDB::operations::RemoveColumn::isValid(gen : RDB::ModelGeneration) : Boolean {
    // sloupec existuje v dané tabulce
	return gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.name)
	// daný sloupec nemá žádná omezení
		   and gen.findColumn(self.owningSchemaName, self.owningTableName, self.name).constraints->size() = 0
	// pro daný sloupec neexistuje tabulkové omezení 'Unique'
		   and not gen.isUnique(self.owningSchemaName, self.owningTableName, self.name)
	// pro daný sloupec neexistuje tabulkové omezení 'PrimaryKey'
		   and not gen.isPrimaryKey(self.owningSchemaName, self.owningTableName, self.name)
	// pro daný sloupec neexistuje tabulkové omezení 'ForeignKey'
		   and not gen.isForeignKey(self.owningSchemaName, self.owningTableName, self.name);
}

// odstraní atribut z dané tøídy
mapping RDB::operations::RemoveColumn::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	// odstranìní sloupce
	t.columns := t.columns->excluding(gen.findColumn(self.owningSchemaName, self.owningTableName, self.name));
}

query RDB::operations::RemoveTableConstraint::isValid(gen : RDB::ModelGeneration) : Boolean {
    // omezení existuje pro danou tabulku
	return gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name);
}

// odstraní omezení pro danou tabulku
mapping RDB::operations::RemoveTableConstraint::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	// odstranìní omezení
	t.constraints := t.constraints->excluding(gen.findTableConstraint(self.owningSchemaName, self.owningTableName, self.name));
}

query RDB::operations::RemoveColumnConstraint::isValid(gen : RDB::ModelGeneration) : Boolean {
    // omezení existuje pro daný sloupec
	return gen.isConstraintForColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName, self.name);
}

// odstraní omezení pro daný sloupec
mapping RDB::operations::RemoveColumnConstraint::apply(inout gen : RDB::ModelGeneration) {
	var c : RDB::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName);
	// odstranìní omezení
	c.constraints := c.constraints->excluding(gen.findColumnConstraint(self.owningSchemaName, self.owningTableName, self.owningColumnName, self.name));
}

query RDB::operations::RemoveIndex::isValid(gen : RDB::ModelGeneration) : Boolean {
    // index existuje v daném schématu
	return gen.isIndexInSchema(self.owningSchemaName, self.name);
}

// odstraní index z daného schématu
mapping RDB::operations::RemoveIndex::apply(inout gen : RDB::ModelGeneration) {
	var s : RDB::Schema := gen.findSchema(self.owningSchemaName);
	// odstranìní index
	s.indexes := s.indexes->excluding(gen.findIndex(self.owningSchemaName, self.name));
}

/* ++++++++++ QUERIES FOR OPERATIONS ++++++++++++++++++++ */

// ovìøí, zda-li existuje schéma v generaci
query RDB::ModelGeneration::isSchemaInGeneration(sName : String) : Boolean {
	return self.schemas->exists(name = sName);
}

// vrátí schéma z generace
query RDB::ModelGeneration::findSchema(sName : String) : RDB::Schema {
	return self.schemas->selectOne(name = sName);
}

// ovìøí, zda-li existuje sekvence ve schématu
query RDB::ModelGeneration::isSequenceInSchema(sName : String, qName : String) : Boolean {
	return self.findSchema(sName).sequences->exists(name = qName);
}

// ovìøí, zda-li existuje index ve schématu
query RDB::ModelGeneration::isIndexInSchema(sName : String, iName : String) : Boolean {
	return self.findSchema(sName).indexes->exists(name = iName);
}

// vrátí index ze schéma
query RDB::ModelGeneration::findIndex(sName : String, iName : String) : RDB::Index {
	return self.findSchema(sName).indexes->selectOne(name = iName);
}

// ovìøí, zda-li existuje tabulka ve schématu
query RDB::ModelGeneration::isTableInSchema(sName : String, tName : String) : Boolean {
	return self.findSchema(sName).tables->exists(name = tName);
}

// vrátí tabulku ze schéma
query RDB::ModelGeneration::findTable(sName : String, tName : String) : RDB::Table {
	return self.findSchema(sName).tables->selectOne(name = tName);
}

// ovìøí, zda-li existuje sloupec v tabulce
query RDB::ModelGeneration::isColumnInTable(sName : String, tName : String, cName : String) : Boolean {
	return self.findTable(sName, tName).columns->exists(name = cName);
}

// ovìøí, zda-li existují sloupce v tabulce
query RDB::ModelGeneration::areColumnsInTable(sName : String, tName : String, cNames : OrderedSet(String)) : Boolean {
	var b : Boolean := true;
	cNames->forEach(cName) {
		b = b and self.isColumnInTable(sName, tName, cName);
	};
	return b;
}

// vrátí sloupec z tabulky
query RDB::ModelGeneration::findColumn(sName : String, tName : String, cName : String) : RDB::Column {
	return self.findTable(sName, tName).columns->selectOne(name = cName);
}

// vrátí seøazenou množinu sloupcù z tabulky
query RDB::ModelGeneration::findColumns(sName : String, tName : String, cNames : OrderedSet(String)) : OrderedSet(RDB::Column) {
	var columns : OrderedSet(RDB::Column);
	cNames->forEach(cName) {
		columns += self.findColumn(sName, tName, cName);
	};
	return columns;
}

// ovìøí, zda-li existuje omezení pro tabulku
query RDB::ModelGeneration::isConstraintForTable(sName : String, tName : String, rName : String) : Boolean {
	return self.findTable(sName, tName).constraints->exists(name = rName);
}

// ovìøí, zda-li existuje omezení pro sloupec 
query RDB::ModelGeneration::isConstraintForColumn(sName : String, tName : String, cName : String, rName : String) : Boolean {
	return self.findColumn(sName, tName, cName).constraints->exists(name = rName);
}

// vrátí omezení dané tabulky
query RDB::ModelGeneration::findTableConstraint(sName : String, tName : String, rName : String) : RDB::TableConstraint {
	return self.findTable(sName, tName).constraints->selectOne(name = rName);
}

// vrátí omezení daného sloupce
query RDB::ModelGeneration::findColumnConstraint(sName : String, tName : String, cName : String, rName : String) : RDB::ColumnConstraint {
	return self.findColumn(sName, tName, cName).constraints->selectOne(name = rName);
}

// ovìøí, zda-li existuje pro sloupec tabulkové omezení 'Unique'
query RDB::ModelGeneration::isUnique(sName : String, tName : String, cName : String) : Boolean {
	// vybere omezení typu 'Unique'
	self.findTable(sName, tName).constraints.oclAsType(RDB::UniqueIndex)->forEach(r) {
		return r.underlyingIndex.columns->exists(name = cName);
	};
	return false;
}

// ovìøí, zda-li existuje pro sloupce tabulkové omezení 'PrimaryKey'
query RDB::ModelGeneration::isPrimaryKey(sName : String, tName : String, cName : String) : Boolean {
	// vybere omezení typu 'PrimaryKey'
	self.findTable(sName, tName).constraints.oclAsType(RDB::PrimaryKey)->forEach(r) {
		return r.underlyingIndex.columns->exists(name = cName);
	};
	return false;
}

// ovìøí, zda-li existuje pro sloupec tabulkové omezení 'ForeignKey'
query RDB::ModelGeneration::isForeignKey(sName : String, tName : String, cName : String) : Boolean {
	// vybere omezení typu 'ForeignKey'
	self.findTable(sName, tName).constraints.oclAsType(RDB::ForeignKey)->forEach(r) {
		return r.constrainedColumn.name = cName;
	};
	return false;
}
